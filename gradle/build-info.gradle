println 'Java: ' + System.getProperty('java.version') + ', JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + '), Arch: ' + System.getProperty('os.arch')

static def getEnvAsBoolean(String name) {
    return System.getenv(name) && System.getenv(name).toBoolean()
}

def exec(String... commands) {
    return providers.exec {
        commandLine(commands)
    }.standardOutput.asText.get().trim()
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

def isCi = getEnvAsBoolean('CI')
def isGithubActions = isCi && getEnvAsBoolean('GITHUB_ACTIONS')
def hasGit = project.file('.git').isDirectory()

def reversion = isGithubActions
        ? System.getenv('GITHUB_SHA')
        : (hasGit ? exec('git', 'rev-parse', '--verify', 'HEAD') : 'unknown')
def reversionShort = reversion.length() > 8 ? reversion.substring(0, 7) : reversion
def buildNumber = isGithubActions ? System.getenv('GITHUB_RUN_NUMBER').toInteger() : 0
def isDirty = (!isCi && hasGit) ? !exec('git', 'status', '--short').isEmpty() : false
def isRelease = isGithubActions ? getEnvAsBoolean('IS_RELEASE') : false
def buildInfo = "rev.${reversionShort}-build.${buildNumber}${isRelease ? '' : '-dev'}${isDirty ? '-dirty' : ''}"

def projectFullName = "${mod_id}-${minecraft_version}-${mod_version}-${buildInfo}"
println "Project: " + projectFullName

project.group = mod_group_id
project.base {
    archivesName = mod_id
}
project.version = getEnvAsBoolean('IS_RELEASE')
        ? "${minecraft_version}-${mod_version}"
        : "${minecraft_version}-${mod_version}-${buildInfo}"

def packageName = "${mod_group_id}.${mod_id}"
def buildInfoClass = """package ${packageName};
// ! THIS FILE IS AUTOGENERATED !
// To make changes, please edit the template in the build script.
// (typically the template can be found in "build-info.gradle")
public final class BuildInfo {
    public static final String VERSION = "${mod_version}";
    public static final String REVERSION = "${reversion}";
    public static final int BUILD_NUMBER = ${buildNumber};
    public static final boolean IS_CI_BUILD = ${isCi};
    public static final boolean IS_RELEASE = ${isRelease};
    public static final boolean IS_DIRTY = ${isDirty};
    public static final String MINECRAFT_VERSION = "${minecraft_version}";
    public static final String NEOFORGE_VERSION = "${neo_version}";
}
"""

sourceSets.main.java { srcDir "$buildDir/generated/java" }

tasks.register('generateBuildInfo') {
    outputs.upToDateWhen { false }
    outputs.cacheIf { false }
    doLast {
        def dir = "$buildDir/generated/java/${packageName.replace('.', '/')}"
        file(dir).mkdirs()
        file("$dir/BuildInfo.java").write(buildInfoClass)
        file("$buildDir/tmp").mkdirs()
        file("$buildDir/tmp/artifact-name.txt").write(projectFullName)
    }
}

compileJava.dependsOn generateBuildInfo

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
